USE [SAC]
GO
/****** Object:  Trigger [dbo].[trSolicitarProrrogacaoPrazoCaptacao_Insert]    Script Date: 28/04/2015 17:34:23 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER OFF
GO

ALTER  TRIGGER [dbo].[trSolicitarProrrogacaoPrazoCaptacao_Insert] ON [dbo].[vwSolicitarProrrogacaoPrazoCaptacao]
	INSTEAD OF INSERT

AS

-- ============================================================================================
-- Autor: Rômulo Menhô Barbosa
-- Data de Criação: 10/01/2013
-- Descrição: Incluir a solicitação do novo período para captar recursos. (NOVOSALIC)
-- ============================================================================================
-- Autor: Jorge Arruda / Gabriel
-- Data de Criação: 10/04/2015
-- Descrição: Deletar a linha 82 que contém o seguinte código RAISERROR('O seu pedido de prorrogação foi recebido no MinC, aguarde a avaliação.<p>',16,1,@Erro)
-- Motivo: Quando o proponente vem requerir a prorrogação do prazo de captação de recursos, a aplicação lança
-- um erro, porque esta linha da trigger lança um "erro" com a mensagem de sucesso, o Zend entende que este erro
-- que o banco lança com a mensagem de "sucesso" é um erro. Por esse motivo a aplicação quebra.
-- ========================================================================================

DECLARE @Erro             int
DECLARE @Rows             int
DECLARE @Flag             bit
DECLARE @DtInicio         datetime
DECLARE @DtFim            datetime
DECLARE @DtInicioExecucao datetime
DECLARE @DtFimExecucao    datetime
DECLARE @Situacao         char(3)
DECLARE @Mecanismo        char(1)
DECLARE @idPronac         int
DECLARE @Pronac           varchar(7)
DECLARE @AnoProjeto       char(2)
DECLARE @Sequencial       varchar(5)
DECLARE @Justificativa    varchar(250)
DECLARE @Restricao        bit
DECLARE @Mensagem         varchar(500)
DECLARE @UltimaData       varchar(10)
DECLARE @Atendimento      char(1)

SET @Flag = 0
SET @Restricao = 0
SET @Mensagem = ''

--===================================================================================================================================
-- RECUPERAR INFORMAÇÕES
--===================================================================================================================================

SELECT @idPronac = idPronac,@Pronac = AnoProjeto + Sequencial, @AnoProjeto = AnoProjeto, @Sequencial = Sequencial, 
       @DtInicio = DtInicio, @DtFim = DtFinal, @Justificativa = Justificativa, @DtFimExecucao = DtFimExecucao
       FROM Inserted

--===================================================================================================================================
-- VERIFICAR OS PROJETOS QUE PODEM SER VALIDADOS PELOS SISTEMA
--===================================================================================================================================
IF EXISTS(SELECT * FROM Projetos 
                   WHERE Mecanismo = '1' and Situacao in ('E10','E12') and Orgao in (272,166) and IdPRONAC = @idPronac and
                         DateDiff(dy,dbo.fnDtPortariaPublicacao(AnoProjeto,Sequencial),convert(char(8),convert(char(4),year(DATEADD(year,1,getdate()))) + '0101' ,112)) <= 730)
   BEGIN
     SET @Atendimento = 'N'
   END
ELSE
   BEGIN
     SET @Atendimento = 'A'
   END
--===================================================================================================================================
-- INSERIR REGISTRO NA TABELA DE PRORROGAÇÃO.
--===================================================================================================================================
IF @Flag = 0
   BEGIN 
     INSERT INTO dbo.Prorrogacao 
                (idPronac,AnoProjeto,Sequencial,DtPedido,DtInicio,DtFinal,Diligenciado,Observacao,Atendimento,Logon,idDocumento) 
          SELECT idPronac,AnoProjeto,Sequencial,getdate(),DtInicio,DtFinal,'N',@Justificativa,@Atendimento,idUsuario,idDocumento FROM Inserted
   
     SELECT @Rows = @@ROWCOUNT, @Erro = @@ERROR
     
     IF @Erro <> 0
        BEGIN
          RAISERROR('2. Erro ao INCLUIR registro %d na tabela Prorrogação, transação cancelada.',16,1,@Erro)
          ROLLBACK TRAN
          RETURN
        END
     
     IF @Rows <> 1
        BEGIN
          RAISERROR('3. Não é permitido INCLUIR %d registros ao mesmo tempo na tabela Prorrogação, transação cancelada',16,1,@Rows)
          ROLLBACK TRAN
          RETURN
        END
     
     --===================================================================================================================================
     -- INSERIR OUTRO REGISTRO CORRESPONDENTE AO PRÓXIMO EXERCÍCIO SE O PERÍODO DE CAPTAÇÃO SE ENCERRAR APOS 31/10 ATÉ 30/12.
     --===================================================================================================================================
     IF YEAR(@DtFimExecucao) > YEAR(GETDATE()) AND 
        (DATEDIFF(dy,dbo.fnFimCaptacao(@AnoProjeto,@Sequencial),convert(char(8),convert(char(4),YEAR(GETDATE())) + '1230' ,112)) > 0 and
         DATEDIFF(dy,dbo.fnFimCaptacao(@AnoProjeto,@Sequencial),convert(char(8),convert(char(4),YEAR(GETDATE())) + '1230' ,112)) <= 60) 
        BEGIN
           IF YEAR(@DtFimExecucao) > YEAR(dateadd(year,1,GETDATE()))
              BEGIN 
                INSERT INTO dbo.Prorrogacao 
                           (idPronac,AnoProjeto,Sequencial,DtPedido,DtInicio,DtFinal,Diligenciado,Observacao,Atendimento,Logon) 
                     SELECT idPronac,AnoProjeto,Sequencial,dateadd(MINUTE,1,GETDATE()),
                            CONVERT(DATETIME,CONVERT(char(4),YEAR(DATEADD(YEAR,1,GETDATE()))) + '0101'),
                            CONVERT(DATETIME,CONVERT(char(4),YEAR(DATEADD(YEAR,1,GETDATE()))) + '1231'),
                            'N',@Justificativa,@Atendimento,idUsuario 
                       FROM Inserted
              END
           ELSE
              BEGIN 
                INSERT INTO dbo.Prorrogacao 
                           (idPronac,AnoProjeto,Sequencial,DtPedido,DtInicio,DtFinal,Diligenciado,Observacao,Atendimento,Logon,idDocumento) 
                     SELECT idPronac,AnoProjeto,Sequencial,dateadd(MINUTE,1,GETDATE()),
                            CONVERT(DATETIME,CONVERT(char(4),YEAR(DATEADD(YEAR,1,GETDATE()))) + '0101'),@DtFimExecucao,
                            'N',@Justificativa,@Atendimento,idUsuario,idDocumento 
                       FROM Inserted
              END
           
          SELECT @Rows = @@ROWCOUNT, @Erro = @@ERROR
     
          IF @Erro <> 0
             BEGIN
               RAISERROR('2. Erro ao INCLUIR registro %d na tabela Prorrogação, transação cancelada.',16,1,@Erro)
               ROLLBACK TRAN
               RETURN
             END
     
          IF @Rows <> 1
             BEGIN
               RAISERROR('3. Não é permitido INCLUIR %d registros ao mesmo tempo na tabela Prorrogação, transação cancelada',16,1,@Rows)
               ROLLBACK TRAN
               RETURN
             END
        END
     END
ELSE
   RAISERROR(@Mensagem,16,1,@Erro)
   
 
